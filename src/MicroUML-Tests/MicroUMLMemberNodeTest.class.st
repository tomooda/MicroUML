Class {
	#name : 'MicroUMLMemberNodeTest',
	#superclass : 'TestCase',
	#instVars : [
		'member'
	],
	#category : 'MicroUML-Tests',
	#package : 'MicroUML-Tests'
}

{ #category : 'testing' }
MicroUMLMemberNodeTest class >> isAbstract [

	^ self = MicroUMLMemberNodeTest
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testArgumentTypes [

	member argumentTypes: #( Type1 Type2 ).
	self assert: member argumentTypes equals: { #Type1. #Type2 }
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testBeClassSide [

	self deny: member isClassSide.
	member beClassSide.
	self assert: member isClassSide
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testBeInstanceSide [

	member beClassSide.
	self deny: member isInstanceSide.
	member beInstanceSide.
	self assert: member isInstanceSide
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testIsAttribute [

	self subclassResponsibility
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testIsMethod [

	self subclassResponsibility
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testIsPrivate [

	member visibility: #public.
	self deny: member isPrivate.
	member visibility: #private.
	self assert: member isPrivate
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testIsPublic [

	member visibility: #public.
	self assert: member isPublic.
	member visibility: #private.
	self deny: member isPublic
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testModifiers [

	self deny: member isAbstract.
	member modifiers: #( abstract ).
	self assert: member isAbstract.
	self assert: member modifiers equals: #( abstract )
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testName [

	member name: #foo.
	self assert: member name equals: #foo
]

{ #category : 'utilities' }
MicroUMLMemberNodeTest >> testPrintMicroUML: aString [

	| association generatedSource1 generatedSource2 |
	association := (OpalCompiler new evaluate: aString) currentClass
		               currentMember.
	generatedSource1 := association microUMLSource.
	association := (OpalCompiler new evaluate: aString) currentClass
		               currentMember.
	generatedSource2 := association microUMLSource.
	self assert: generatedSource2 equals: generatedSource1
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testPrintMicroUMLOn [

	self
		testPrintMicroUML:
			'MicroUMLAstBuilder === #Foo - #bar @ String % #abstract %#private';
		testPrintMicroUML:
			'MicroUMLAstBuilder === #Foo > #bar ~#(Integer Boolean) @ String % #abstract %#private';
		testPrintMicroUML:
			'MicroUMLAstBuilder === #Foo |- #bar @ String % #abstract %#private';
		testPrintMicroUML:
			'MicroUMLAstBuilder === #Foo |> #bar ~#(Integer Boolean) @ String % #abstract %#private'
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testType [

	member type: #Foo.
	self assert: member type equals: #Foo
]

{ #category : 'tests' }
MicroUMLMemberNodeTest >> testVisibility [

	member visibility: #public.
	self assert: member visibility equals: #public.
	member visibility: #private.
	self assert: member visibility equals: #private
]
